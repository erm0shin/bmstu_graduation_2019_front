allprojects {
    ext.configureNodePlugin = {
        apply plugin: 'com.github.node-gradle.node'

        node {
            // Version of node to use
            version = '10.15.3'

            // Base URL for fetching node distributions (change if you have a mirror)
            distBaseUrl = 'https://nodejs.org/dist'

            // If true, it will download node using above parameters.
            // If false, it will try to use globally installed node.
            download = true

            // Set the work directory for unpacking node
            workDir = file("${projectDir}/build/nodejs")

            // Set the work directory for NPM
            npmWorkDir = file("${projectDir}/build/npm")

            // Set the work directory where node_modules should be located
            nodeModulesDir = file("${projectDir}/build")
        }
    }

    ext.applyKarma = {
        apply plugin: 'kotlin-dce-js'
        configureNodePlugin()

        runDceJsTestKotlin.dceOptions.devMode = true
        runDceJsTestKotlin.dependsOn(jsTestClasses, npmInstall)

        task copyKarmaConfig(dependsOn: [runDceJsTestKotlin]) {
            doLast {
                copy {
                    from ("${projectDir}") {
                        include "karma.conf.js"
                        include "webpack.karma.js"
                    }

                    into "${projectDir}/build"
                }
            }
        }

        task runKarmaTests(type: NodeTask, dependsOn: [runDceJsTestKotlin, copyKarmaConfig]) {
            script = file("${projectDir}/build/node_modules/karma/bin/karma")
            args = ['start', '--single-run', '--browsers', 'ChromeHeadlessNoSandbox', file("${projectDir}/build/karma.conf.js")]

            execOverrides {
                it.workingDir = file("${projectDir}/build/")
            }
        }
    }
}